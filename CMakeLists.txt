cmake_minimum_required(VERSION 3.15)

project(nlpp LANGUAGES C CXX)

set(lib_path ${CMAKE_SOURCE_DIR}/nlpp)
set(CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/cmake)

include(GNUInstallDirs)
include(CMakePackageConfigHelpers)

include(get_project_version.cmake)
include(handle_dependency.cmake)

getProjectVersion(nlpp/helpers/config.hpp)

if(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE Release CACHE STRING "" FORCE)
endif()

## Options
option(NLPP_USE_NANOFLANN "If Nanoflann will be used" ON)

option(NLPP_BUILD_CPP_LIB "If the C++ libraries will be generated by cmake" OFF)
option(NLPP_BUILD_PYTHON_LIB "If the Python libraries will be generated by cmake" OFF)
option(NLPP_BUILD_JS_LIB "If the JavaScript libraries will be generated by cmake" OFF)

option(NLPP_BUILD_TESTS "If the unit test will be generated by cmake" OFF)
option(NLPP_BUILD_EXAMPLES "If the examples will be generated by cmake" OFF)
option(NLPP_BUILD_DOCS "If the docs will be generated by cmake" OFF)
option(NLPP_BUILD_BENCH "If the docs will be generated by cmake" OFF)
###


handleDependency(Eigen3 Eigen)
handleDependency(Spectra spectra)

set(HANDY_BuildExamples OFF)
set(HANDY_BuildTests OFF)
handleDependency(Handy handy)

if(${NLPP_USE_NANOFLANN})
    handleDependency(nanoflann nanoflann)
endif()

add_subdirectory(${lib_path})



if(NLPP_BUILD_CPP_LIB)
    add_subdirectory(lib/cpp)
endif()

if(NLPP_BUILD_PYTHON_LIB)
    if(NOT NLPP_BUILD_CPP_LIB)
        message(FATAL_ERROR "The C++ libs need to be built in order to build the Python libs")
    endif()

    add_subdirectory(lib/python)
endif()

if(NLPP_BUILD_JS_LIB)
    if(NOT NLPP_BUILD_CPP_LIB)
        message(FATAL_ERROR "The C++ libs need to be built in order to build the JavaScript libs")
    endif()

    add_subdirectory(lib/js)
endif()


if(NLPP_BUILD_TESTS)
    add_subdirectory(tests)

    # # Workaround for the Spectra lib
    # if(${EIGEN_INCLUDE_LOCAL})
    #     target_include_directories(tests PUBLIC ${PROJECT_SOURCE_DIR}/include/nlpp/external/Eigen)
    # endif()
endif()

if(NLPP_BUILD_EXAMPLES)
    add_subdirectory(examples)
endif()

if(NLPP_BUILD_DOCS)
    add_subdirectory(doc)
endif()

if(NLPP_BUILD_BENCH)
    add_subdirectory(benchmark)
endif()
